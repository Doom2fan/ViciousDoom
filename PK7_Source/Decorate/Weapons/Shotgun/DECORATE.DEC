actor VD_ShotgunClip : ammo {
    inventory.MaxAmount 9
    ammo.BackpackMaxAmount 9
    +ignoreSkill
}

actor VD_Shotgun : VD_BaseWeapon {
    weapon.AmmoUse 1
    weapon.AmmoType "VD_ShotgunClip"
    weapon.AmmoType2 "VD_Shells"
    weapon.AmmoGive2 15
    obituary "$OB_MPSHOTGUN"
    tag "$TAG_SHOTGUN"
    states {
    Ready:
        SHTS CBA 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("VD_DoMelee", 1, "KickMelee")
        SHTG A 1 A_WeaponReady (WRF_AllowReload)
        Loop

    Deselect:
        SHTS ABC 1
        goto BaseDeselect

    Fire:
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound ("Weapons/Shotgun/Fire", CHAN_Weapon)
        TNT1 A 0 A_GunFlash
        TNT1 AAAAAAAA 0 A_FireBullets (2.5, 3.2, -1, random (1, 3) * 12, "VD_BulletPuff", FBF_NoFlash | FBF_NoRandom)
        SHTG A 3 A_FireBullets (2.5, 3.2, -1, random (1, 3) * 12, "VD_BulletPuff", FBF_UseAmmo | FBF_NoFlash | FBF_NoRandom)
        SHTG ABCDDDCBAA 3
        SHTG A 1 A_Refire
        Goto Ready2
    Flash:
        SHTF A 1
        stop

    Reload:
        TNT1 A 0 A_JumpIfInventory ("VD_ShotgunClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("VD_Shells", 1, "TrueReloadStart")
        goto Ready2
    TrueReloadStart:
        TNT1 A 0 A_JumpIfInventory ("VD_ShotgunClip", 1, "TrueReload")
        TNT1 A 0 A_JumpIfInventory ("VD_Shells", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("VD_ShotgunClip", 1)
        TNT1 A 0 A_TakeInventory ("VD_Shells", 1)
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("VD_ShotgunClip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("VD_Shells", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("VD_ShotgunClip", 1)
        TNT1 A 0 A_TakeInventory ("VD_Shells", 1)
        loop
    ReloadFinish:
        goto Ready2

    Spawn:
        SHTG Z -1
        stop
    }
}