/* Credits:
** Decorate: WildWeasel, Chronos "phantombeta" Ouroboros
** Sprites: ???
** Weapons: A_FireCustomMissile (tracerName, fRandom [weaponSpread] (-, horizontalSpread), 1, horizontalOffset, verticalOffset, 0, fRandom [weaponSpread] (-, verticalSpread))
** Monsters: A_CustomMissile (tracerName, verticalOffset, horizontalOffset, fRandom [monsterSpread] (-, horizontalSpread), 0, fRandom [monsterSpread] (-, verticalSpread))
*/

/*** Base ***/
actor VD_TracerEffectsBase {
    height 2
    radius 2
    alpha 1.0
    scale 0.025
    renderStyle add
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly +forceXYBillboard
}

actor VD_BaseTracer : fastProjectile {
    alpha 1.0
    renderStyle add
    radius 2
    height 2
    speed 25
    args 100, 1, 8, 1, 3
    // args [speed, numBullets, damage, damageRandomMin, damageRandomMax]
    projectile
    +bloodSplatter +noExtremeDeath +noDamageThrust +puffGetsOwner
    -noBlockmap
    damage (random (1, 3) * 6)
    decal Bullet
    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A 0 A_ScaleVelocity (args [0] * 1.0)
        TNT1 A 0 A_Jump (256, "Spawn2")
        wait
    Spawn2:
        TNT1 A 0 A_SetPitch (CallACS ("VD_GetProjectilePitch") / 65535.0)
        TNT1 A 0 A_CustomBulletAttack (0.0, 0.0, args [1], args [2] * random (args [3], args [4]), "VD_BulletPuff", 32767.0, CBAF_AimFacing | CBAF_NoRandom | CBAF_ExplicitAngle)
        TNT1 A 0 A_SetPitch (0)
        TNT1 A 0 A_Jump (256, "Idle")
        wait
    Idle:
        SPRK A 1 bright
        wait
    Death:
        TNT1 A 1
        stop
    }
}
actor VD_BulletPuff : VD_TracerEffectsBase replaces BulletPuff {
    +puffGetsOwner
    -clientsideOnly -noInteraction
    states {
    Spawn:
        TNT1 AAAAA 0 noDelay A_SpawnItemEx ("VD_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (0.0, 3.0), 0, SXF_Clientside)
        TNT1 A 1 A_SpawnItemEx ("VD_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (0.0, 3.0), 0, SXF_Clientside)
        stop
    }
}

/*actor VD_BaseTracerPlayer : VD_BaseTracer {
    states {
    Spawn2:
        TNT1 A 0 ACS_NamedExecuteWithResult ("VD_SetProjectilePitch")
        TNT1 A 0 ACS_NamedExecuteWithResult ("AAAA")
        TNT1 A 0 A_CustomBulletAttack (0.0, 0.0, args [1], args [2] * random (args [3], args [4]), "VD_BulletPuffPlayer", 32767.0, CBAF_AimFacing | CBAF_NoRandom | CBAF_ExplicitAngle)
        TNT1 A 0 A_SetPitch (0)
        TNT1 A 0 A_Jump (256, "Idle")
        wait
    }
}
actor VD_BulletPuffPlayer : VD_BulletPuff { +ThruGhost }*/

actor VD_BulletPuffSparks : VD_TracerEffectsBase {
    mass 1
    damage 0
    +missile +dropOff +ghost +dontSplash
    -doomBounce -noGravity -noInteraction
    speed 10
    gravity 1.0
    states {
    Spawn:
        SPRK A 1 bright
        SPRK A 1 bright A_FadeOut (0.05)
        wait
    }
}