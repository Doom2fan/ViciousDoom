// Credits:
// Decorate: WildWeasel, Chronos "phantombeta" Ouroboros
// Sprites: ???
// Weapons: A_FireCustomMissile (tracerName, fRandom [weaponSpread] (-, horizontalSpread), 1, horizontalOffset, verticalOffset, 0, fRandom [weaponSpread] (-, verticalSpread))
// Monsters: A_CustomMissile (tracerName, verticalOffset, horizontalOffset, fRandom [monsterSpread] (-, horizontalSpread), 0, fRandom [monsterSpread] (-, verticalSpread))

/*** Base ***/
actor VD_TracerEffectsBase {
    height 2
    radius 2
    alpha 1.0
    scale 0.025
    renderStyle add
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly +forceXYBillboard
}

actor VD_BaseTracer : fastProjectile {
    alpha 1.0
    renderStyle add
    radius 2
    height 2
    speed 25
    Health 75
    projectile
    +bloodSplatter +noExtremeDeath +noDamageThrust +puffGetsOwner
    +forceXYBillboard
    scale 0.025
    damage (0)
    decal Bullet
    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A 0 A_ScaleVelocity (health * 1.0)
        SPRK A 1 bright
        wait
    Death:
    Crash:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_SpawnItemEx ("VD_BulletPuff")
        stop
    XDeath:
        TNT1 A 3 A_PlaySound ("Bullet/HitFlesh", CHAN_Body, 0.5)
        stop
    }
}

actor VD_BulletPuff : VD_TracerEffectsBase replaces BulletPuff {
    -clientsideOnly
    states {
    Spawn:
        TNT1 AAAAA 0 noDelay A_SpawnItemEx ("VD_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (0.0, 3.0), 0, SXF_Clientside)
        SPRK A 1 bright A_SpawnItemEx ("VD_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (0.0, 3.0), 0, SXF_Clientside)
        stop
    }
}

actor VD_BulletPuffSparks : VD_TracerEffectsBase {
    mass 1
    damage 0
    +missile +dropOff +ghost +dontSplash
    -doomBounce -noGravity -noInteraction
    speed 10
    gravity 1.0
    states {
    Spawn:
        SPRK A 1 bright
        SPRK A 1 bright A_FadeOut (0.05)
        wait
    }
}